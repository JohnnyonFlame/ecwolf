actor AngelOfDeath 27
{
	radius 42
	points 5000
	health 1450, 1550, 1650, 2000
	speed 1, 3
	missilechance 8
	MONSTER
	+AMBUSH
	states
	{
		Spawn:
			ANGL A -1 NOP A_Look(0, 0, 0, 0, 360)
			stop
		See:
			ANGL A 5 NOP T_Chase(CHF_BACKOFF)
			ANGL A 1.5
			ANGL B 4 NOP T_Chase(CHF_BACKOFF)
			ANGL C 5 NOP T_Chase(CHF_BACKOFF)
			ANGL C 1.5
			ANGL D 4 NOP T_Chase(CHF_BACKOFF)
			loop
		Missile:
			ANGL E 5
			ANGL F 10 A_StartAttack
		Launch:
			ANGL E 5 A_CustomMissile("Spark")
			ANGL E 0 A_Relaunch
			goto Launch
		Tired:
			// TODO: When sound attenuation is supported, make this play at full volume.
			ANGL G 20 A_PlaySound("angel/breath")
			ANGL H 20
			ANGL G 20 A_PlaySound("angel/breath")
			ANGL H 20
			ANGL G 20 A_PlaySound("angel/breath")
			ANGL H 20
			ANGL G 20 A_PlaySound("angel/breath")
			goto See
		Death:
			ANGL A .5 A_Fall
			ANGL A .5 A_DeathScream
			ANGL I 5
			ANGL J 5 A_PlaySound("misc/slurpie")
			ANGL KLMNO 5
			ANGL P -1 A_Victory
			stop
	}
}

actor Spark
{
	speed 16
	damage (random[MissileDamage](30, 62))
	seesound "angel/attack"
	PROJECTILE
	states
	{
		Spawn:
			SPRK ABCD 3
			loop
	}
}
